module volgrid6d_alchimia_class

USE volgrid6d_class
USE alchimia
USE grid_id_class

implicit NONE

interface make
   module procedure  make_vg6d
end interface

private
public make

contains

subroutine make_vg6d(mayvfn,mybin,mybout,vg6din,vg6dout)
type(fndsv),intent(inout) :: mayvfn
character(len=*),intent(in) :: mybin(:),mybout(:)
type(volgrid6d),intent(in) :: vg6din
type(volgrid6d),intent(out) :: vg6dout
integer :: i,nx,ny,nlevel,ntime,ntimerange,nvar,nvarin,ilevel,itime,itimerange,ivar,ivarin,ivarout
real,allocatable :: myin(:,:),myout(:,:)

nx=size(vg6din%voldati,1)
ny=size(vg6din%voldati,2)
nlevel=size(vg6din%voldati,3)
ntime=size(vg6din%voldati,4)
ntimerange=size(vg6din%voldati,5)
nvarin=size(mybin)
nvar=size(mybout)

allocate(myout(nx*ny,nvar))

call init(vg6dout, vg6din%griddim, vg6din%time_definition, categoryappend="generated by alchimia make")
call volgrid6d_alloc(vg6dout, vg6din%griddim%dim, ntime, nlevel, ntimerange, nvar)
call volgrid6d_alloc_vol(vg6dout,inivol=.true.)

vg6dout%time=vg6din%time
vg6dout%timerange=vg6din%timerange
vg6dout%level=vg6din%level

do i=size(mayvfn%fnds),1,-1
  if (c_e(mayvfn%fnds(i))) then
    do ilevel=1,nlevel
      do itime=1,ntime
        do itimerange=1,ntimerange
          myin=reshape(vg6din%voldati(:,:,ilevel,itime,itimerange,:),(/nx*ny,nvarin/))
          myout=rmiss
          call mayvfn%fnds(i)%fn(mybin,mybout,mayvfn%fnds(i)%bin,mayvfn%fnds(i)%bout,myin,myout)
          vg6dout%voldati(:,:,ilevel,itime,itimerange,:)=reshape(myout,(/nx,ny,nvar/))
          do ivar=1, size(mayvfn%fnds(i)%bout)
            ivarin  = index(mybin,mayvfn%fnds(i)%bin(1))
            ivarout = index(mybout,mayvfn%fnds(i)%bout(ivar))
            call copy (vg6din%gaid(ilevel,itime,itimerange,ivarin), vg6dout%gaid(ilevel,itime,itimerange,ivarout))

#ifdef HAVE_LIBGRIBAPI
            call grib_set(grid_id_get_gaid(vg6dout%gaid(ilevel,itime,itimerange,ivarout)),"bitsPerValue",24)
#endif

          end do
        end do
      end do
    end do
  end if
end do

end subroutine make_vg6d

end module volgrid6d_alchimia_class
